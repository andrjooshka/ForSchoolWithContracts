description = "execounting application"

apply plugin: "war"
apply plugin: "java"
apply plugin: "jetty"
apply plugin: "idea"

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

group = "tap"
version = "4.8.1"


repositories {
    mavenCentral()

    // All things JBoss/Hibernate
    mavenRepo name: "JBoss", url: "http://repository.jboss.org/nexus/content/groups/public/"

    // For stable versions of the tapx libraries
    mavenRepo name: "HLS", url: "http://howardlewisship.com/repository/"

    // For non-stable versions of the tapx libraries
    mavenRepo name: "HLS Snapshots", url: "http://howardlewisship.com/snapshot-repository/"

    // For access to Apache Staging (Preview) packages
    mavenRepo name: "Apache Staging", url: "https://repository.apache.org/content/groups/staging"
}

// This simulates Maven's "provided" scope, until it is officially supported by Gradle
// See http://jira.codehaus.org/browse/GRADLE-784

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}

dependencies {

	// Tapestry things
	// Tapestry release version 5.3.6
    compile "org.apache.tapestry:tapestry-core:5.3.6"
	compile "org.apache.tapestry:tapestry-beanvalidator:5.3.6"
    // This adds automatic compression of JavaScript and CSS in production mode:
    compile "org.apache.tapestry:tapestry-yuicompressor:5.3.6"
	
	// Hibernate things
	compile "org.apache.tapestry:tapestry-hibernate:5.3.6"
	compile "org.hibernate:hibernate-validator:4.3.0.Final"
	// C3P0
    compile "org.hibernate:hibernate-c3p0:3.6.0.Final"
    compile "c3p0:c3p0:0.9.1"
    
    // MYSQL
    compile "mysql:mysql-connector-java:5.1.21"
    
    // Tagselect ( multiple names for event visitors )
    compile "se.unbound:tapestry-tagselect:1.4.1"
    // Exanpe components ( project uses accordeons from that library )
    compile "fr.exanpe:exanpe-t5-lib:1.2.1"

	// MD5 function provider
	compile "commons-codec:commons-codec:1.7"

    provided "javax.servlet:servlet-api:2.5"
}

test {
    useTestNG()

    options.suites("src/test/conf/testng.xml")

    systemProperties["tapestry.service-reloading-enabled"] = "false"
    systemProperties["tapestry.execution-mode"] = "development"

    maxHeapSize = "600M"

    jvmArgs("-XX:MaxPermSize=256M")

    enableAssertions = true
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("build/classes/main")
        testOutputDir = file('build/classes/test')

        downloadJavadoc = true
        downloadSources = true
    }
}